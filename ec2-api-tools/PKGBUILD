# Contributor: Nick Lanham (nick AT afternight DOT org)
# Contributor: Yejun Yang (yejunx AT gmail DOT com)
# Contributor: vwyodajl <donjuansjiz   GmaIL   com>
# Maintainer: adminempire <jl adminempire com>
### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=ec2-api-tools
pkgver=1.6.12.0
pkgrel=1
pkgdesc="The API tools that serve as the client interface to the Amazon EC2 web service"
arch=('any')
url="http://aws.amazon.com/developertools/351"
source=("http://ec2-downloads.s3.amazonaws.com/$pkgname-$pkgver.zip")
depends=(java-runtime)
license=('custom')
md5sums=('5289f874d2abd84080e957f61c18bb92')

package() {
	cd "$srcdir"/"$pkgname"-"$pkgver"
	mkdir -p "$pkgdir"/usr/lib
	# install lib i.e jar file
	cp -r lib "$pkgdir"/usr/lib/ec2-api-tools
	find "$pkgdir" -type f -exec chmod 644 {} \;

	# fix env. var. in scripts
	sed -i 's#:?EC2_HOME is not set#:=/usr#' bin/*
	sed -i -e 's#:-EC2_HOME is not set#:=/usr#' \
		-e 's#:?JAVA_HOME is not set#:-/usr#' \
		-e 's#LIBDIR="${EC2_HOME}/lib"#LIBDIR="${EC2_HOME}/lib/ec2-api-tools"#' bin/ec2-cmd
	# and install scripts
	cp -r bin "$pkgdir"/usr
	# Remove unneeded windows cmd files
	rm "$pkgdir"/usr/bin/*.cmd

	# install license files
	mkdir -p "$pkgdir"/usr/share/licenses/"$pkgname"/
	install -m 644 license.txt "$pkgdir"/usr/share/licenses/"$pkgname"/
	install -m 644 THIRDPARTYLICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/
}

